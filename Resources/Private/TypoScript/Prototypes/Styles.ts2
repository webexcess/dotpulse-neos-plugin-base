prototype(Base:CSSCollection) < prototype(TypoScript:Collection) {
	@context.loadCSSAsynchron = ${Configuration.setting('Dotpulse.Base.Source.loadCSSAsynchron')}

	itemName = 'item'
	itemRenderer = TypoScript:Collection {
		collection = ${item ? String.split(String.replace(String.trim(item), ']', ''), '[') : []}
		iterationName = 'iterator'
		itemName = 'entry'
		itemRenderer = TypoScript:Case {
			isFilename {
				condition = ${iterator.isFirst}
				renderer = TypoScript:Case {
					isExternal {
						condition = ${String.indexOf(entry, '//') != -1}
						renderer = ${entry}
					}
					isInternal {
						condition = ${entry}
						renderer = TypoScript:ResourceUri {
							path = ${'resource://' + sitePackage + '/Public/Styles/' + entry +'.css'}
						}
					}
					@process.wrap = ${' href="' + value + VersionOfAssets + '"'}
				}
			}
			areAttributes {
				condition = ${entry}
				renderer = ${' ' + String.trim(entry)}
			}
		}
		@process.wrap = ${value ? loadCSSAsynchron ? '<link rel="preload"' + value + ' as="style" onload="this.rel=\'stylesheet\'" /><noscript><link rel="stylesheet"' + value + ' /></noscript>' : '<link rel="stylesheet"' + value + ' />' : ''}
	}
}
