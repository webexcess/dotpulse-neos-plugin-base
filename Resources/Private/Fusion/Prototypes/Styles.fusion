prototype(Dotpulse.Base:CSSCollection) < prototype(Neos.Fusion:Collection) {
	@context.loadCSSAsynchron = ${Configuration.setting('Dotpulse.Base.Source.loadCSSAsynchron')}

	itemName = 'item'
	itemRenderer = Neos.Fusion:Collection {
		collection = ${item ? String.split(String.replace(String.trim(item), ']', ''), '[') : []}
		iterationName = 'iterator'
		itemName = 'entry'
		itemRenderer = Neos.Fusion:Case {
			isFilename {
				condition = ${iterator.isFirst}
				renderer = Neos.Fusion:Case {
					isExternal {
						condition = ${String.indexOf(entry, '//') != -1}
						renderer = ${entry}
					}
					isInternal {
						condition = ${entry}
						renderer = Neos.Fusion:ResourceUri {
							path = ${'resource://' + sitePackage + '/Public/Styles/' + entry +'.css'}
						}
					}
					@process.wrap = ${' href="' + value + VersionOfAssets + '"'}
				}
			}
			areAttributes {
				condition = ${entry}
				renderer = ${' ' + String.trim(entry)}
			}
		}
		@process.wrap = ${value ? loadCSSAsynchron ? '<link rel="preload"' + value + ' as="style" onload="this.rel=\'stylesheet\'" /><noscript><link rel="stylesheet"' + value + ' /></noscript>' : '<link rel="stylesheet"' + value + ' />' : ''}
	}
}
